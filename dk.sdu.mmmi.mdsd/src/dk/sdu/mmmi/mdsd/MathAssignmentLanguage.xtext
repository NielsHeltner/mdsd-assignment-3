grammar dk.sdu.mmmi.mdsd.MathAssignmentLanguage with org.eclipse.xtext.common.Terminals

generate mathAssignmentLanguage "http://www.sdu.dk/mmmi/mdsd/MathAssignmentLanguage"

Root:
	elements+=Element*
;

Element:
	EvaluateExpression | ExternalDeclaration
;

EvaluateExpression:
	'result' label=STRING 'is' expression=Expression
;

ExternalDeclaration:
	'external' name=ID '(' parameters+=Parameter (',' parameters+=Parameter)* ')'
;

Parameter:
	type=ID name=ID
;

Expression:
	AdditionOrSubtraction
;

AdditionOrSubtraction returns Expression:
	MultiplicationOrDivision (
		({Addition.left=current} '+' | {Subtraction.left=current} '-' ) 
		right=MultiplicationOrDivision)*
;

MultiplicationOrDivision returns Expression:
	Primary (
		({Multiplication.left=current} '*' | {Division.left=current} '/' ) 
		right=Primary)*
;

Primary returns Expression:
	Literal | VariableDeclaration | VariableReference | ExternalReference | Parenthesis
;

Literal:
	value=INT
;

VariableDeclaration:
	'let' name=ID '=' expression=Expression
	(in=In)? 'end'
;

In:
	'in' expression=Expression
;

VariableReference:
	variable=[VariableDeclaration]
;

ExternalReference:
	external=[ExternalDeclaration] '(' arguments+=Expression (',' arguments+=Expression)* ')'
;

Parenthesis returns Expression:
	'(' Expression ')'
;

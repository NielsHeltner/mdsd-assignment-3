/*
 * generated by Xtext 2.12.0
 */
package dk.sdu.mmmi.mdsd.generator

import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.Addition
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.Division
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.EvaluateExpression
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.ExternalDeclaration
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.ExternalReference
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.Literal
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.Multiplication
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.Parameter
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.Root
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.Subtraction
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.VariableDeclaration
import dk.sdu.mmmi.mdsd.mathAssignmentLanguage.VariableReference
import java.util.ArrayList
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext

import static extension org.eclipse.xtext.EcoreUtil2.getAllContainers
import static extension org.eclipse.xtext.EcoreUtil2.getAllContentsOfType

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class MathAssignmentLanguageGenerator extends AbstractGenerator {
	
	public static val GEN_FILE_EXT = ".java"
	public static val GEN_DIR = "math/"
	public static val GEN_FILE_NAME = "MathComputation"
	
	var Root root

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		root = resource.allContents.filter(Root).head
		
		val dir = GEN_DIR
		val pkg = dir.replaceAll("/", ".").substring(0, dir.length - 1) // convert path to package by converting all '/' to '.', and remove trailing '.'
		val fileName = GEN_FILE_NAME
		
		fsa.generateFile(dir + fileName + GEN_FILE_EXT, root.generateClass(pkg, fileName))
	}
    
	def generateClass(Root root, String pkg, String name)'''
		«generateHeader»
		package «pkg»;
		
		public class «name» {
		
			public static interface Externals {
				
				«FOR external : root.elements.filter(ExternalDeclaration)»
					«external.generateMethod»;
					
				«ENDFOR»
			}
			
			private Externals externals;
			
			public «name»(Externals _externals) {
				externals = _externals;
			}
			
			public void compute() {
				«FOR evaluate : root.elements.filter(EvaluateExpression)»
					«evaluate.generateComputation»
				«ENDFOR»
			}
			
			«root.generateNestedInnerClass»
		}
	'''
	
	/**
	 * Iterates through an EObject's variable declaration children and generates nested inner classes for them.
	 */
	def <T extends EObject> generateNestedInnerClass(T parent)'''
		«FOR declaration: parent.directVariableDeclarations»
			«declaration.generateInnerClass»
			
		«ENDFOR»
	'''
	
	/**
	 * Recursively generates inner classes for variable declarations.
	 */
	def CharSequence generateInnerClass(VariableDeclaration declaration)'''
		class «declaration.generateInnerClassName» {
			
			private final int «declaration.name» = «declaration.generateAssignment»;
			
			public int compute() {
				return «declaration.in.generate»;
			}
			
			«declaration.generateNestedInnerClass»
		}
	'''
	
	/**
	 * Ensures that the generated code for an expression of the type:
	 * 		let x = 5 in let x = x
	 * can be resolved properly, by telling Java it should look in the outer class for the last 'x'.
	 */
	def generateAssignment(VariableDeclaration dec) { // TODO: refactor this method
		/*val expression = head.assignment
		val declarations = variableDeclarations.get(variableDeclarations.getIndex(head).key)
		expression.getAllContentsOfType(VariableReference).filter[variable.name == head.name].forEach[
			val ref = it
			val target = declarations.takeWhile[it !== head].findLast[name == ref.variable.name]
			variable.name = '''«target.generateInnerClassName».this.«variable.name»'''
		]*/
		//expression.generate
		
		
		//bug: hvis expression i sig selv er en VariableReference så kommer den ikke med i forEach
		dec.assignment.getAllContentsOfType(VariableReference).filter[variable.name == dec.name].forEach[
			var candidate = dec
			var VariableDeclaration target
			while (target === null) {
				if (candidate != dec && candidate.name == variable.name) {
					target = candidate
					variable.name = '''«target.generateInnerClassName».this.«variable.name»'''
				}
				candidate = candidate.parent
			}
		]
		dec.assignment.generate
	}
	
	def generateInnerClassName(VariableDeclaration declaration) {
		val name = root.indexOf(declaration).toString.replace("->", "_")
		'''Let«name»'''
	}
	
	def generateMethod(ExternalDeclaration dec)
		'''public int «dec.generateMethodSignature»'''
	
	def generateMethodSignature(ExternalDeclaration dec)
		'''«dec.name»«dec.parameters.generateParameters»'''
	
	def generateParameters(Parameter... params)
		'''(«FOR param : params SEPARATOR ', '»«param.type» «param.name»«ENDFOR»)'''
	
	def generateComputation(EvaluateExpression evaluate)
		'''System.out.println(«evaluate.generate»);'''
	
	def generateHeader()'''
		/**
		 * Generated by MathAssignmentLanguage
		 */
 	'''
	
	// Start of recursive multi-dispatch methods for displaying an arithmetic expression's complete syntax tree
	
	def dispatch CharSequence generate(EvaluateExpression element)
		'''"«element.label» " + «element.expression.generate»'''
	
	def dispatch CharSequence generate(Addition expression)
		'''(«expression.left.generate» + «expression.right.generate»)'''
	
	def dispatch CharSequence generate(Subtraction expression)
		'''(«expression.left.generate» - «expression.right.generate»)'''
	
	def dispatch CharSequence generate(Multiplication expression)
		'''(«expression.left.generate» * «expression.right.generate»)'''
	
	def dispatch CharSequence generate(Division expression)
		'''(«expression.left.generate» / «expression.right.generate»)'''
	
	def dispatch CharSequence generate(VariableDeclaration declaration)
		'''new «declaration.generateInnerClassName»().compute()'''
	
	def dispatch CharSequence generate(VariableReference reference)
		'''«reference.variable.name»'''
	
	def dispatch CharSequence generate(ExternalReference reference)
		'''externals.«reference.external.name»(«FOR argument : reference.arguments SEPARATOR ', '»«argument.generate»«ENDFOR»)'''
	
	def dispatch generate(Literal expression)
		'''«expression.value»'''
	
	// Start of methods for navigating the tree structure of the metamodel.
	
	/**
     * Recursively searches through all children in the input object for the target VariableDeclaration 
     * using a depth-first search.
     * 
     * Returns nested pairs of integers representing the nested and parallel structure
     * of the target VariableDeclaration's location in the tree structure.
	 */
    def <T extends EObject> indexOf(T input, VariableDeclaration target) {
    	val children = input.eAllContents
		var outerIndex = 0
    	while (children.hasNext) {
    		val candidate = children.next
    		if (candidate === target) {
    			return outerIndex
    		}
    		if (candidate instanceof VariableDeclaration || candidate instanceof EvaluateExpression) {
    			children.prune // removes all elements nested in the last result of ::next (but keeps those parallel)
    			val innerIndex = candidate.indexOf(target)
    			if (innerIndex !== null) {
    				return outerIndex -> innerIndex
    			}
    			outerIndex++
    		}
		}
    }
    
    /**
     * Returns the first VariableDeclaration found, and any VariableDeclarations that are
     * parallel / at the same depth of nesting as the first one found.
     */
    def <T extends EObject> getDirectVariableDeclarations(EObject input) {
    	val children = input.eAllContents
    	val results = new ArrayList()
    	while (children.hasNext) {
    		val candidate = children.next
    		if (candidate instanceof VariableDeclaration) {
    			children.prune // removes all elements nested in the last result of ::next (but keeps those parallel)
    			results.add(candidate)
    		}
    	}
    	return results
    }
    
    /**
     * Returns the closest container of type VariableDeclaration for the input.
     */
    def getParent(VariableDeclaration input) {
    	input.allContainers.filter(VariableDeclaration).head
    }
	
}